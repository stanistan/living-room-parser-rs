// There should only be *one* main entry function.
//
// The organization of this grammar is top-level -> down,
// so that the last rules should be literals/terminals
// and the top first rule is the public/exported main
// `parse` function.

pub parse -> Vec<Term<'input>> = ts:term* {
    ts
}

term -> Term<'input>
    = value / hole / id / variable / wildcard / ws / word

value -> Term<'input>
    = float / int / null / boolean / string

string -> Term<'input>
    = "\"" s:$(!"\"" !"\n" .)* "\"" {
        Term::String { value: s }
    }

word -> Term<'input>
    = w:$(word_pattern+) ![0-9] {
        Term::Word { word: w }
    }
    / w:$(word_char+) {
        Term::Word { word: w }
    }

int -> Term<'input>
    = "0" { Term::Int { value: 0 } }
    / s:$([-+]?[1-9]digit*) {
        Term::Int { value: i64::from_str(s).unwrap() }
    }

float -> Term<'input>
    = s:$( [-+]? digit* frac exp? ) {
        Term::Float { value: f64::from_str(s).unwrap() }
    }
    / s:$( [-+]? frac exp?) {
        Term::Float { value: f64::from_str(s).unwrap() }
    }

null -> Term<'input>
    = "null" { Term::Null { value: None } }

boolean -> Term<'input>
    = "true" {
        Term::Bool { value: true }
    }
    / "false" {
        Term::Bool { value: false }
    }

id -> Term<'input>
    = "#" w:$(alnum*) {
        Term::Id { id: w }
    }

variable -> Term<'input>
    = "$" w:$(alnum+) {
        Term::Variable { variable: w }
    }

ws -> Term<'input>
    = whitespace+ {
        Term::Word { word: " " }
    }

hole -> Term<'input>
    = "_" {
        Term::Hole { hole: true }
    }

wildcard -> Term<'input>
    = "$" {
        Term::Wildcard { wildcard: true }
    }

alnum = digit / character
word_delimiter = "$" / "#" / "_" / whitespace / digit
word_char = [^# $ 0-9 _ . "]
word_pattern = word_char+ "."+

exp = ("e" / "E") ("-" / "+")? digit*<1,>
frac = "." digit*<1,>

whitespace = "\n" / " " / "\n\r" / "\t"
digit = [0-9]
character = [a-zA-Z]
